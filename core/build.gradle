apply plugin: "com.android.library"

version = "1.0.1-SNAPSHOT"

dependencies {
	testImplementation 'junit:junit:4.13.2'
}

buildscript {
	repositories {
		mavenCentral()
		google()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:3.6.4'
		classpath "org.jacoco:org.jacoco.core:0.8.5"
	}
}

android {

	compileSdkVersion = 29

	// http://developer.android.com/tools/revisions/build-tools.html
	buildToolsVersion = "29.0.2"

	defaultConfig {
		minSdkVersion 21
		targetSdkVersion 29
	}

	compileOptions {
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
	}

	testOptions {
		unitTests {
			returnDefaultValues = true
			includeAndroidResources = true
		}
	}
}

apply plugin: "maven-publish"

task sourceJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs, android.sourceSets.release.java.srcDirs
	archiveClassifier = "sources"
}

afterEvaluate {
	publishing {

		publications {
			releaseAndroidLibrary(MavenPublication) {
				artifact(sourceJar)
				artifact tasks.getByName("bundleReleaseAar")
			}
		}
		repositories {
			maven {
				name = "GitHubPackages"
				url = project.findProperty("github_packages_registry_url")
				credentials {
					username = System.getenv("GH_USER_NAME")
					password = System.getenv("GH_PACKAGES_UPLOAD_TOKEN")
				}
			}
		}
	}
}

